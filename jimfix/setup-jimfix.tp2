BACKUP ~Jimfix/backup~
AUTHOR ~anotherdole@gmail.com~
VERSION ~v2.5~

README ~jimfix/readme/readme.txt~

LANGUAGE ~English~     ~english~     ~jimfix/languages/english/setup.tra~

BEGIN @10
DESIGNATED 0
GROUP @1
COPY_EXISTING ~spwi523.spl~ ~override~
  LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 12 power = 5 END
BUT_ONLY
ACTION_IF FILE_EXISTS_IN_GAME ~dw#0w523.spl~ BEGIN
  COPY_EXISTING ~dw#0w523.spl~ ~override~
    LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 12 power = 5 END
  BUT_ONLY
END

BEGIN @11
DESIGNATED 1
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~tobex_ini/tobexcore.ini~ @28
COPY_EXISTING ~contingx.2da~ ~override~
  REPLACE_TEXTUALLY ~SPWI510~ ~****   ~
BUT_ONLY

BEGIN @12
DESIGNATED 2
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS ~override/dw#pro_NM.mrk~ @29
COPY_EXISTING ~shld24.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 197 target = 1 parameter2 = IDS_OF_SYMBOL(projectl "dw#flam") timing = 2 END
BUT_ONLY

BEGIN @13
DESIGNATED 3
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~abazdg02.cre~ @30
REQUIRE_PREDICATE FILE_EXISTS ~override/dw#ascension_abazigal.mrk~ @31
COPY_EXISTING ~dragblue.cre~ ~override~
  WRITE_ASCII 0x0250 ~dragblue~
BUT_ONLY
COPY_EXISTING ~dragblue.bcs~ ~override~
  REPLACE_BCS_BLOCK ~jimfix/baf/abazconting.baf~ ~jimfix/baf/abazcontingtamah.baf~
BUT_ONLY

BEGIN @14
DESIGNATED 4
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~finiren.cre~ @30
REQUIRE_PREDICATE FILE_EXISTS ~override/dw#ascension_sendai_irenicus.mrk~ @32
COPY_EXISTING ~finiren.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,WIZARD_MISLEAD)~ ~ReallyForceSpellRES("dw#0w607",Myself)~
  END
BUT_ONLY

BEGIN @15
DESIGNATED 5
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~finsolar.cre~ @30
REQUIRE_PREDICATE FILE_EXISTS ~override/dw#celestial.mrk~ @33
COPY_EXISTING ~finsol04.bcs~ ~override~
  REPLACE_BCS_BLOCK ~jimfix/baf/solarbad.baf~ ~jimfix/baf/empty.baf~
BUT_ONLY

BEGIN @16
DESIGNATED 100
GROUP @1
INCLUDE ~jimfix/lib/schoolcheck.tph~
COPY_EXISTING ~SPPR604.SPL~ ~override~ //fix vanilla bug with this spell
  WRITE_BYTE 0x25 2 //set its school to conjuration
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~SPWI[0-9]*\.spl\|SPPR[0-9]*\.spl~ ~override~
  READ_SHORT 0x22 cast_anim
  READ_BYTE  0x25 sp_school
  PATCH_IF cast_anim > 8 && cast_anim < 17 && sp_school > 0 && sp_school < 9 THEN BEGIN
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 1 anim_num = 12 STR_VAR school_name = "Abjuration" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 2 anim_num = 14 STR_VAR school_name = "Conjuration" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 3 anim_num = 16 STR_VAR school_name = "Divination" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 4 anim_num = 11 STR_VAR school_name = "Enchantment" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 5 anim_num = 13 STR_VAR school_name = "Illusion" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 6 anim_num = 15 STR_VAR school_name = "Evocation" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 7 anim_num =  9 STR_VAR school_name = "Necromancy" END
    LPF ~CHECK_ANIMATION~ INT_VAR school_num = 8 anim_num = 10 STR_VAR school_name = "Alteration" END
  END
BUT_ONLY

BEGIN @17
DESIGNATED 400
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS ~override/dw#mage.mrk~ @34
ACTION_FOR_EACH filename IN ~dw#sw102~ ~dw#sw408~ ~dw#sw319~ ~dw#sw606~ ~dw#sw512~ ~dw#sw517~ ~dw#sw320~ 
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%filename%.spl~ BEGIN
    COPY_EXISTING ~%filename%.spl~ ~override~
      READ_LONG 0x8 spell_name
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 parameter1 = spell_name header = 1 insert_point = 0 END
    BUT_ONLY
  END
END

BEGIN @18
DESIGNATED 201
GROUP @2
SUBCOMPONENT @23
INCLUDE ~jimfix/lib/schoolnotes.tph~
LAF ADD_SCHOOL_NOTIFICATIONS INT_VAR threshold = 1 END

BEGIN @19
DESIGNATED 202
GROUP @2
SUBCOMPONENT @23
INCLUDE ~jimfix/lib/schoolnotes.tph~
LAF ADD_SCHOOL_NOTIFICATIONS INT_VAR threshold = 2 END

BEGIN @20
DESIGNATED 203
GROUP @2
SUBCOMPONENT @23
INCLUDE ~jimfix/lib/schoolnotes.tph~
LAF ADD_SCHOOL_NOTIFICATIONS INT_VAR threshold = 3 END

BEGIN @21
DESIGNATED 204
GROUP @2
SUBCOMPONENT @23
INCLUDE ~jimfix/lib/schoolnotes.tph~
LAF ADD_SCHOOL_NOTIFICATIONS INT_VAR threshold = 4 END

BEGIN @22
DESIGNATED 205
GROUP @2
SUBCOMPONENT @23
INCLUDE ~jimfix/lib/schoolnotes.tph~
LAF ADD_SCHOOL_NOTIFICATIONS INT_VAR threshold = 5 END

BEGIN @24
DESIGNATED 300
GROUP @2
INCLUDE ~jimfix/lib/expirations.tph~

BEGIN @25
DESIGNATED 600
GROUP @2
INCLUDE ~jimfix/lib/itemexpirations.tph~

BEGIN @26
DESIGNATED 500
GROUP @2
COPY_EXISTING_REGEXP GLOB ~.*\.are~ ~override~
	READ_LONG 0xa8	door_offset
	READ_LONG 0xa4	door_count
	FOR(door_num = 0; door_num < door_count; door_num+=1) BEGIN
		READ_LONG (door_offset + (0xc8 * door_num) + 0x28) door_flags
		PATCH_IF (door_flags & 0b10000000) BEGIN
			WRITE_LONG (door_offset + (0xc8 * door_num) + 0x28) (door_flags | 0b100000000)
		END
	END
BUT_ONLY

BEGIN @27 
DESIGNATED 700
GROUP @2
//Fix wish breach
COPY_EXISTING ~DW#WISH1.SPL~ ~override/JF#WISHA.SPL~
	LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END
IF_EXISTS
COPY_EXISTING ~SPWI513C.SPL~ ~override/JF#WISHB.SPL~
IF_EXISTS
ACTION_FOR_EACH filename IN ~DW#WISH1.SPL~ ~SPWISH26.SPL~ ~SPWISH38.SPL~ BEGIN
	COPY_EXISTING ~%filename%~ ~override~
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = ~-1~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~JF#WISHA~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~JF#WISHB~ END
	IF_EXISTS
END
ACTION_FOR_EACH filename IN ~SPWI519.SPL~ ~DW#0W519.SPL~ ~DW#SW519.SPL~ BEGIN
	COPY_EXISTING ~%filename%~ ~override~
		FOR(level = 0; level < 10; level = level + 1) BEGIN
			dur = 198 + level * 18
			LPF ADD_SPELL_EFFECT INT_VAR header = (level + 1) opcode = 206 target = 1 power = 5 duration = dur STR_VAR resource = ~JF#WISHA~ END
		END
	IF_EXISTS
END
//Fix disjunction
ACTION_IF FILE_EXISTS_IN_GAME ~DW#SOLDI.SPL~ BEGIN
	COPY_EXISTING ~JF#WISHB.SPL~ ~override/JF#DISJB.SPL~
		LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END
		WRITE_LONG 0x34 8
	IF_EXISTS
	COPY_EXISTING ~DW#SOLDI.SPL~ ~override/JF#DISJA.SPL~
		LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END
	IF_EXISTS
	COPY_EXISTING ~DW#SOLDI.SPL~ ~override~
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = ~-1~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~JF#DISJA~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~JF#DISJB~ END
	IF_EXISTS
	ACTION_FOR_EACH filename IN ~SPWI519.SPL~ ~DW#0W519.SPL~ ~DW#SW519.SPL~ BEGIN
		COPY_EXISTING ~%filename%~ ~override~
			FOR(level = 0; level < 10; level = level + 1) BEGIN
				dur = 198 + level * 18
				LPF ADD_SPELL_EFFECT INT_VAR header = (level + 1) opcode = 206 target = 1 power = 5 duration = dur STR_VAR resource = ~JF#DISJA~ END
			END
		IF_EXISTS
	END
END